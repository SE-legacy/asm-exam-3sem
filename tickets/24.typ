= Передача параметров в подпрограммы по ссылке и по значению.

При передаче параметров *по значению* процедура работает с копией данных, и входные параметры, хранящиеся в памяти, не изменяются.

```asm
; Процедура, определяющее наибольшее из двух чисел. Одно число передается через регистр AX, другое --- через BX.
; Результат возвращается через регистр AX.

Max proc
    cmp AX, BX
    jge e1
    mov AX, BX
e1:
    ret
Max endp

; Вызов

mov AX, a 
mov BX, b
call Max
```

При передаче параметров *по ссылке* процедура получает адрес области памяти, где хранится значение, поэтому в него можно внести изменения.

```asm
; Процедура, выполняющая операцию x = x / 16, где x хранится в области памяти по адресу [BX]. (x --- машинное слово)
Proc_div16 proc
    push CX
    mov CL, 4
    shr word ptr [BX], CL   ; x = x / 16 
    pop CX
    ret
Proc_div16 endp

; Вызов:
lea BX, a 
call Proc_div16
```

Массивы, располагающиеся в блоке данных, удобно передавать процедуре по ссылке на их начало.

Ниже пример подпрограммы, находящей максимальный элемент в массиве беззнаковых байтов. Она принимает адрес начала массива через регистр BX, количество элементов массива через регистр CX, а результат возвращает в регистре AL.

```asm
Max proc
    push BX ; Сохраним значения в стеке на случай, если вызывающей программе не нужно
    push CX ; чтобы мы их изменяли
    
    xor AL, AL ; Текущее максимльное значение = 0
l1:
    cmp [BX], AL
    jna c1     ; Если элемент не больше AL, то продолжать
    mov AL, [BX]
c1:
    inc BX     ; перейти к следующему элементу
    loop l1

    pop CX
    pop BX
    ret
Max endp
```
